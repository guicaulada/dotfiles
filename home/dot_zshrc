# Docker desktop added completions
fpath=($HOME/.docker/completions $fpath)

# Antidote plugin manager
source "$HOMEBREW_PREFIX/opt/antidote/share/antidote/antidote.zsh"
antidote load

# Completions (after antidote so all plugins' fpath entries are available)
autoload -Uz compinit
compinit

# zoxide (smarter cd)
eval "$(zoxide init zsh)"

# Starship (prompt)
eval "$(starship init zsh)"

# mise (runtime version manager)
eval "$(mise activate zsh)"

# direnv (per-directory environments)
eval "$(direnv hook zsh)"

# thefuck (lazy-loaded for faster startup)
fuck() {
  unfunction fuck
  eval "$(thefuck --alias)"
  fuck "$@"
}

# History
HISTFILE=~/.zsh_history
HISTSIZE=50000
SAVEHIST=50000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_EXPIRE_DUPS_FIRST
setopt SHARE_HISTORY
setopt HIST_REDUCE_BLANKS

# Shell options
setopt AUTO_CD
setopt NO_CLOBBER
setopt EXTENDED_GLOB

# Aliases
alias vim=nvim
alias cat='bat --paging=never'
alias ls='eza --icons'
alias ll='eza -la --icons --git'
alias lt='eza --tree --icons --level=2'

# Git helpers
gcnov() {
  git commit --no-verify "$@"
}

gpsu() {
  git push --set-upstream origin "$(git branch --show-current)" "$@"
}

grtg() {
  echo "This will delete and recreate tag '$1', then force-push to origin."
  read -q "REPLY?Continue? [y/N] " || { echo; return 1; }
  echo
  git tag -d "$1"
  git tag "$1"
  git push origin -f "$1"
}

# Ghostty tab title (append to Ghostty's hooks to guarantee running last)
_ghostty_title_hook() {
  if (( $+functions[_ghostty_precmd] )); then
    functions[_ghostty_precmd]+='
      builtin print -Pn "\e]0;%1~\a"'
    functions[_ghostty_preexec]+='
      builtin print -Pn "\e]0;%1~\a"'
    print -Pn "\e]0;%1~\a"
    add-zsh-hook -d precmd _ghostty_title_hook
  fi
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd _ghostty_title_hook
